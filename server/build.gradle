plugins {
    id "org.springframework.boot"
    id "org.jetbrains.kotlin.plugin.spring"
    id "org.jetbrains.kotlin.plugin.jpa"
    id "org.openapi.generator"
    id "com.gorylenko.gradle-git-properties"
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-validation"

    implementation "org.postgresql:postgresql"
    implementation "org.flywaydb:flyway-core"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.testcontainers:postgresql:$postgresContainerVersion"
}

openApiGenerate {
    generatorName = "kotlin-spring"
    templateDir = "$rootDir/openapi/server/templates"
    configFile = "$rootDir/openapi/server/config.yml"
    ignoreFileOverride = "$rootDir/openapi/server/.openapi-generator-ignore"

    inputSpec = "$rootDir/openapi/servers.yml"
    outputDir = "$buildDir/generated"
}

runKtlintCheckOverMainSourceSet.dependsOn(tasks.openApiGenerate)
compileKotlin.dependsOn(tasks.openApiGenerate)

sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated/src/main/kotlin"
        }
    }
}

gitProperties {
    dateFormat = "dd.MM.yyyy HH:mm:ss"
    dateFormatTimeZone = "Europe/Moscow"
    extProperty = "gitProps"
}

generateGitProperties.outputs.upToDateWhen { false }

bootJar {
    archiveFileName = "${project.name}.jar"

    manifest {
        attributes(
            "Application-Name": "${project.name}",
            "Application-Version": "${project.version}",
            "Git-Branch": "${-> project.ext.gitProps["git.branch"]}",
            "Build-Revision": "${-> project.ext.gitProps["git.commit.id.abbrev"]}",
            "Commit-Time": "${-> project.ext.gitProps["git.commit.time"]}"
        )
    }
}

jar {
    enabled = false
}
